plugins {
    id "java"
    id "com.dorongold.task-tree" version "1.3.1"
    id 'org.gradle.test-retry' version '1.0.0'
}

sourceCompatibility = 1.8

repositories {
    jcenter()

    // If `mavenLocal()` is placed before `jcenter()`,
    // you can see the source code for libraries in IntelliJ.
    // Why?
    mavenLocal()
}

dependencies {
    implementation platform("com.fasterxml.jackson:jackson-bom:2.10.1")

    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-smile")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-xml")

    implementation("com.google.guava:guava:$guavaVersion")

    implementation("org.apache.avro:avro:${avroVersion}")
    implementation("org.apache.commons:commons-lang3:${commonsLang3Version}")
    implementation("org.apache.httpcomponents:httpclient:$httpClientVersion")
    implementation("commons-io:commons-io:$commonsIoVersion")
    implementation("org.bouncycastle:bcprov-jdk15on:$bouncyCastleProviderVersion")
    implementation("org.jsoup:jsoup:$jsoupVersion")
    implementation("ch.qos.logback:logback-classic:$logbackVersion")
    implementation("org.apache.kafka:kafka-clients:${kafkaClientVersion}")
    implementation("org.hibernate:hibernate-validator:${hibernateValidatorVersion}")
    implementation("org.glassfish:javax.el:${elVersion}")
    implementation("com.github.javaparser:javaparser-core:${javaParserVersion}")
    implementation("io.netty:netty-all:${nettyVersion}")
    implementation("org.objenesis:objenesis:${objenesisVersion}")
    implementation("com.github.jknack:handlebars:${handlebarsVersion}")
    implementation("com.jayway.jsonpath:json-path:${jsonPathVersion}")
    implementation("org.apache.commons:commons-pool2:${commonsPool2Version}")
    implementation("org.pcollections:pcollections:${pcollectionsVersion}")
    implementation("org.xmlunit:xmlunit-core:${xmlUnitVersion}")
    implementation("org.apache.commons:commons-math3:${commonsMath3Version}")
    implementation("org.ta4j:ta4j-core:${ta4JVersion}")
    implementation("com.opencsv:opencsv:${opencsvVersion}")
    implementation("org.python:jython-standalone:${jythonVersion}")
    implementation("commons-validator:commons-validator:${commonsValidatorVersion}")
    implementation("org.thymeleaf:thymeleaf:${thymeleafVersion}")
    implementation("org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}")
    implementation("com.google.cloud:google-cloud-monitoring:${googleCloudVersion}")
    implementation("io.github.resilience4j:resilience4j-retry:${resilience4JVersion}")

    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")

    testImplementation("org.assertj:assertj-core:$assertJVersion")
    testImplementation("junit:junit:$jUnitVersion")

    // NOTE: Why does `mockito-all` include `org.hamcrest.Matcher`?
    // This leads to `java.lang.NoSuchMethodError: org.hamcrest.Matcher.describeMismatch(Ljava/lang/Object;Lorg/hamcrest/Description;)V`.
//    testImplementation("org.mockito:mockito-all:${mockitoVersion}")
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")

    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

wrapper {
    gradleVersion = '6.2.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation', '-parameters']
}

test {
    retry {
        maxRetries = 3
    }

    testLogging {
        showStandardStreams = true
    }

    // Use a half of CPU cores. Note that it doesn't count hyper-threading as a true processor here.
    maxParallelForks = Runtime.runtime.availableProcessors() / 4
    println "maxParallelForks: " + maxParallelForks
}

task escapeUnicodeInResourceBundles(type: Copy) {
    from 'src/test/resources/learningtest/resource_bundles/sources'
    into 'src/test/resources/learningtest/resource_bundles'
    include '**/*.properties'
    filter(org.apache.tools.ant.filters.EscapeUnicode)
}

processTestResources.dependsOn(escapeUnicodeInResourceBundles)
